find_package(GTest REQUIRED)

set(CURRENT_ROUND "")
set(IGNORED_TASKS "")
include("${CMAKE_SOURCE_DIR}/current.cmake" OPTIONAL)

macro(round NAME)
    if (CURRENT_ROUND STREQUAL "" OR CURRENT_ROUND STREQUAL "${NAME}")
        add_subdirectory("${NAME}")
    else()
        message("Ignoring round \"${NAME}\" since current round is set.")
    endif()
endmacro()

macro(task NAME)
    if (NOT "${NAME}" IN_LIST IGNORED_TASKS)
        add_executable(${NAME} "src/${NAME}.cpp")

        add_executable(${NAME}_test "src/${NAME}.cpp" "${CMAKE_SOURCE_DIR}/common/test.cpp")
        target_compile_definitions(${NAME}_test PRIVATE main=unused_main TASK_NAME=${NAME} "TASK_PATH=\"${CMAKE_CURRENT_SOURCE_DIR}/test/${NAME}\"")
        target_link_libraries(${NAME}_test PRIVATE GTest::Main)
    else()
        message("Ignoring task \"${NAME}\".")
    endif()
endmacro()